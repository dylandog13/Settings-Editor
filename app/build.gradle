apply plugin: 'com.android.application'
apply plugin: 'poeditor'

poeditor {
    // Pull:      ./gradlew poeditorPull
    // Push:      ./gradlew poeditorPush (optional: -Poverride=true)
    // Add terms: ./gradlew poeditorPushTerms

    apikey poeditorAPIKey       // Put 'poeditorAPIKey = <API Key>' in gradle.properties
    projectId poeditorProjectID // Put 'poeditorProjectID = <Project ID>' in gradle.properties

    type 'android_strings'
    terms "app/src/main/res/values/strings.xml"

    def languages = ["en", "nl", "de", "fr", "pt-BR", "ru", "it", "zh-CN", "cs", "tr", "id"]
    languages.each { i ->
        if(i == "en") {
            // English is the default language
            trans i, "app/src/main/res/values/strings.xml"
        } else {
            trans i, "app/src/main/res/values-" + i.split("-")[0] + "/strings.xml"
        }
    }
}

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.3'
    defaultConfig {
        applicationId "com.wilco375.settingseditor"
        minSdkVersion 14
        targetSdkVersion 29
        versionCode 50
        versionName "2.14.1"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }
    lintOptions {
        disable 'MissingTranslation'
        checkReleaseBuilds false
        abortOnError false
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "tier"
    productFlavors {
        free {
            dimension "tier"
            buildConfigField "boolean", "PRO", "false"
            resValue "string", "app_name", "Settings Editor"
        }
        pro {
            dimension "tier"
            applicationId "com.wilco375.settingseditorpro"
            buildConfigField "boolean", "PRO", "true"
            resValue "string", "app_name", "Settings Editor Pro"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude 'androidsupportmultidexversion.txt'
    }
}


dependencies {
    compileOnly fileTree(include: ['*.jar'], dir: 'libs')

    implementation 'com.github.wilco375:DragSortListView:0.6.3'

    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.2.0'
    implementation 'com.android.support:multidex:1.0.3'

    implementation 'com.google.guava:guava:24.1-jre'

    implementation 'com.github.AppIntro:AppIntro:6.0.0'

    implementation 'net.lingala.zip4j:zip4j:1.3.2'

    implementation 'com.fasterxml.jackson.core:jackson-databind:2.8.5'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.8.5'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.8.5'
}